/*
 * Common configuration used by all MIO subprojects.
 */
apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'eclipse'


defaultTasks 'clean', 'buildproject'

ext.vendorName = 'Semagia'
ext.vendorURL = 'http://www.semagia.com/'
ext.licenseName = 'Apache Software License, Version 2.0'
ext.licenseURL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
ext.projectURL = 'http://www.semagia.com/'

jar {
      manifest {
        name = project.ext.projectName
        
        attributes 'Implementation-Version': project.version, 
                   'Implementation-Vendor': project.ext.vendorName, 
                   'Implementation-URL': project.ext.projectURL, 
                   'Sealed': 'false'
                  
        instruction 'Bundle-Vendor', project.ext.vendorName
        instruction 'Bundle-License', project.ext.licenseURL
        instruction 'Bundle-Description', project.ext.projectDescription
        instruction 'Export-Package', '!*.internal.*, *'
      }
}

apply from: "$rootDir/shared/maven.gradle"
apply from: "$rootDir/shared/dist.gradle"

gradle.taskGraph.whenReady {taskGraph ->
   if (!taskGraph.hasTask(':release')) {
       version += '-SNAPSHOT'
   }
}

configurations {
    buildproject {
      extendsFrom compile
    }
}

repositories {
    mavenCentral()
    maven { url 'http://repository.semagia.com/thirdparty/' }
    maven { url 'http://repository.semagia.com/snapshots/' }
}

dependencies {
    compile 'org.slf4j:slf4j-api:(1.5.8,)'
    
    testCompile 'junit:junit:4.11'

    testRuntime 'org.slf4j:slf4j-jdk14:(1.5.8,)'
}

task copyAdditionalResources(type: Copy) {
    from "$rootDir/shared/"
    from "$projectDir"
    into sourceSets.main.output.classesDir
    include('LICENSE.txt')
    include('NOTICE.txt')
}

processResources.dependsOn copyAdditionalResources
